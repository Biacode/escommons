buildscript {
    ext {
        springVersion = '4.3.9.RELEASE'
        elasticSearchVersion = '5.4.0'
        javaxServletApiVersion = '3.1.0'
        javaxRsApiVersion = '2.0.1'
        slfj4ApiVersion = '1.7.25'
        log4jVersion = '2.7'
        jacksonVersion = '2.8.9'
        apacheCommonsLang3Version = '3.6'
        apacheCommonsIoVersion = '2.5'
        jsr305Version = '3.0.2'
        junitVersion = '4.12'
        easymockVersion = '3.4'

        sonarqubeVersion = '2.5'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubeVersion")
    }
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'com.sfl.coolmonkey.escommons'
    version = '0.1.0-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "org.springframework:spring-context:$springVersion"
        compile "org.slf4j:slf4j-api:$slfj4ApiVersion"
        compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        compile "org.apache.commons:commons-lang3:$apacheCommonsLang3Version"
        compile "commons-io:commons-io:$apacheCommonsIoVersion"
        compile "com.google.code.findbugs:jsr305:$jsr305Version"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.easymock:easymock:$easymockVersion"
    }
}

apply plugin: "org.sonarqube"
apply plugin: 'jacoco'

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectName", "escommons"
        property "sonar.projectKey", "escommons"
        property "sonar.organization", "escommons"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
        property "sonar.login", System.getenv("SONAR_TOKEN")
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
        property "sonar.exclusions", ["**com/sfl/coolmonkey/escommons/api/model/**/*", "**com/sfl/coolmonkey/escommons/api/client/**/*"]
    }
}

project(':api:api-rest') {
    dependencies {
        compile project(':core')
        compile project(':api:api-model')
        compile "javax.ws.rs:javax.ws.rs-api:$javaxRsApiVersion"
        compileOnly "javax.servlet:javax.servlet-api:$javaxServletApiVersion"
    }
}

project(':api:api-client') {
    dependencies {
        compile project(':api:api-model')
    }
}

project(':core') {
    dependencies {
        compile "org.elasticsearch:elasticsearch:${elasticSearchVersion}"
        compile "org.elasticsearch.client:transport:${elasticSearchVersion}"
        compile "org.elasticsearch.plugin:transport-netty4-client:${elasticSearchVersion}"
        compile "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    }
}

project(':integration-tests') {
    dependencies {
        compile project(':core')

        compile "org.elasticsearch:elasticsearch:${elasticSearchVersion}"
        compile "org.elasticsearch.client:transport:${elasticSearchVersion}"
        compile "org.elasticsearch.plugin:transport-netty4-client:${elasticSearchVersion}"
        compile "org.apache.logging.log4j:log4j-core:${log4jVersion}"
        testCompile "org.springframework:spring-test:$springVersion"
    }
}